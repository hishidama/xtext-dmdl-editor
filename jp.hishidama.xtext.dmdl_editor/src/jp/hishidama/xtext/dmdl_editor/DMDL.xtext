/*
 * Asakusa Framework DMDL (DataModel Definition Language)
 * 
 * https://github.com/asakusafw/asakusafw/blob/master/dmdl-project/asakusa-dmdl-core/src/main/javacc/DmdlParser.jj
 */
grammar jp.hishidama.xtext.dmdl_editor.DMDL hidden(WS, ML_COMMENT, SL_COMMENT)

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
generate dmdl "http://www.hishidama.jp/xtext/dmdl_editor/dmdl"

Models:
	(list+=ModelDefinition)*;

ModelDefinition:
	RecordModelDefinition;

RecordModelDefinition:
	(description=Description)?
	name=Name '=' rhs=RecordExpression ';';

RecordExpression:
	terms+=RecordTerm
	("+" terms+=RecordTerm)*;

RecordTerm:
	'{'
	(properties+=PropertyDefinition)+
	'}';

PropertyDefinition:
	(description=Description)?
	name=Name ':' type=Type
	';';

Description:
	STRING;

Name:
	NAME_TOKEN;

terminal NAME_TOKEN:
	('a'..'z') ('a'..'z' | '0'..'9')* ('_' ('a'..'z' | '0'..'9')+)*;

/* Basic Types */
enum Type:
	INT | LONG | BYTE | SHORT | DECIMAL | FLOAT | DOUBLE | TEXT | BOOLEAN | DATE | DATETIME;

terminal STRING:
	'"' ('\\' ('b' | 't' | 'n' | 'f' | 'r' | '"' | '\\') | !('\\' | '"'))* '"';

terminal ML_COMMENT:
	'/*'->'*/';

terminal SL_COMMENT:
	('//' | '--') !('\n' | '\r')* ('\r'? '\n')?;

terminal WS:
	(' ' | '\t' | '\r' | '\n')+;
